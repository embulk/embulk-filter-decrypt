plugins {
    id "java"
    id "maven-publish"
    id "checkstyle"
    id "jacoco"
    id "org.embulk.embulk-plugins" version "0.3.1"
}

repositories {
    mavenCentral()
    jcenter()
}

group = "org.embulk.filter.decrypt"
version = "0.1.1"
description = "Decrypt"

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}

dependencies {
    compileOnly "org.embulk:embulk-core:0.9.23"
    compile("com.amazonaws:aws-java-sdk-s3:1.11.253") {
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "joda-time", module: "joda-time"
    }
    compile "org.yaml:snakeyaml:1.18"
    compile "org.embulk:embulk-util-snakeyaml:0.1.0"

    testCompile "junit:junit:4.13"
    testCompile "org.embulk:embulk-core:0.9.23"
    testCompile "org.embulk:embulk-core:0.9.23:tests"
    testCompile "org.embulk:embulk-test:0.9.23"
    testCompile "org.embulk:embulk-standards:0.9.23"
    testCompile "org.embulk:embulk-deps-buffer:0.9.23"
    testCompile "org.embulk:embulk-deps-config:0.9.23"
    testCompile "org.mockito:mockito-core:2.28.2"
}

embulkPlugin {
    mainClass = "org.embulk.filter.decrypt.DecryptFilterPlugin"
    category = "filter"
    type = "decrypt"
}

publishing {
    publications {
        embulkPluginMaven(MavenPublication) {  // Publish it with "publishEmbulkPluginMavenPublicationToMavenRepository".
            from components.java  // Must be "components.java". The dependency modification works only for it.
        }
    }
    repositories {
        // TODO: Configure some public Maven repo(s).
    }
}

gem {
    authors = [ "Vinh Vo" ]
    email = [ "vinhvd@treasure-data.com" ]
    summary = "Decrypt filter plugin for Embulk"
    homepage = "https://github.com/treasure-data/embulk-filter-decrypt"
    licenses = [ "MIT" ]
}

gemPush {
    host = "https://rubygems.org"
}

task downloadDependencies(type: Exec) {
    configurations.testRuntime.files
    commandLine 'echo', 'Downloaded all dependencies'
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

jacocoTestReport {
    group = "Reporting"
    reports {
        xml.setEnabled(false)
        csv.setEnabled(false)
        html.destination file("${buildDir}/reports/coverage")
    }
}
